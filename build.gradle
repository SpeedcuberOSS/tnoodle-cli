plugins {
    // Make an uber/fat executable jar 
    id 'com.github.johnrengelman.shadow' version '6.1.0'

    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    // Bundle a JRE with the distribution
    id 'org.beryx.runtime' version '1.12.7'
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:29.0-jre'
    
    // https://mvnrepository.com/artifact/org.worldcubeassociation.tnoodle/lib-scrambles
    implementation 'org.worldcubeassociation.tnoodle:lib-scrambles:0.19.2'

    // JCommander parses CLI args
    // https://mvnrepository.com/artifact/com.beust/jcommander
    implementation 'com.beust:jcommander:1.81'

    // https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20210307'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13'
}

version = '1.1.0'

application {
    // Define the main class for the application.
    mainClassName = 'tnoodlecli.App'
    applicationName = 'tnoodle'
}

task copyShadowJar(type: Copy) {
    from ("$buildDir/libs/tnoodle-cli-$version-all.jar") {
        rename '(.*)-all(.jar)', '$1$2'
    }
    into "$buildDir/releases/"
}

tasks.named("shadowJar") {
    finalizedBy copyShadowJar
}

// See the docs here: https://badass-runtime-plugin.beryx.org/releases/latest/
runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageDir = file("$buildDir/tnoodle-cli")
    imageZip = file("$buildDir/releases/tnoodle-cli-$version" + ".zip")

    targetPlatform("win_x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.11%2B9_openj9-0.26.0/OpenJDK11U-jdk_x64_windows_openj9_11.0.11_9_openj9-0.26.0.zip")
    }

    targetPlatform("linux_x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.11%2B9_openj9-0.26.0/OpenJDK11U-jdk_x64_linux_openj9_11.0.11_9_openj9-0.26.0.tar.gz")
    }

    targetPlatform("mac_x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.11%2B9_openj9-0.26.0/OpenJDK11U-jdk_x64_mac_openj9_11.0.11_9_openj9-0.26.0.tar.gz")
    }
}